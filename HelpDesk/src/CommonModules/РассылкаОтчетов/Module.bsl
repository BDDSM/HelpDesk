Процедура РассылкаОтчетовПоЗаявкам() Экспорт
	// Получатель отчета
	ПолучателиОтчета = ПолучитьПолучателейОтчетаПоЗаявкам();
	Если ПолучателиОтчета.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	тзВложения = Новый ТаблицаЗначений;
	тзВложения.Колонки.Добавить("Вложение");
	тзВложения.Колонки.Добавить("ИмяФайлаВложения");
	тзВложения.Колонки.Добавить("ИмяВложения");
	
	Отчет = Отчеты.ОтчетПоЗаявкам.Создать();
	ДанныеРасшифровки = Новый ДанныеРасшифровкиКомпоновкиДанных;
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	Результат = Новый ТабличныйДокумент;
	

    Отчет.КомпоновщикНастроек.ЗагрузитьНастройки(Отчет.СхемаКомпоновкиДанных.ВариантыНастроек["ЗаявкиВРаботе"].Настройки);	
	
	МакетКомпоновкиДанных = КомпоновщикМакета.Выполнить(Отчет.СхемаКомпоновкиДанных, Отчет.КомпоновщикНастроек.Настройки, ДанныеРасшифровки);
	
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных,,ДанныеРасшифровки);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(Результат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
	ИмяФайлаОтчета = КаталогВременныхФайлов() + "ОтчетПоЗаявкам.xls";
	
	Результат.Записать(ИмяФайлаОтчета,ТипФайлаТабличногоДокумента.XLS97);
	
	ФайлОтчета = Новый ХранилищеЗначения(Новый ДвоичныеДанные(ИмяФайлаОтчета), Новый СжатиеДанных());
	
	НоваяСтр = тзВложения.Добавить();
	НоваяСтр.ИмяВложения = "ОтчетПоЗаявкам.xls";
	НоваяСтр.Вложение = ФайлОтчета;
	НоваяСтр.ИмяФайлаВложения = ИмяФайлаОтчета;
	СформироватьПисьмо(тзВложения, ПолучателиОтчета);
КонецПроцедуры

Функция СформироватьПисьмо(тзВложения, ПолучателиОтчета)
	ПисьмоОбъект = Документы.ИсходящееПисьмо.СоздатьДокумент();
	
	СтруктураРеквизитов = ПисьмоОбъект.СформироватьСтруктуруРеквизитовПисьма(Неопределено);
	
	ЗаполнитьЗначенияСвойств(ПисьмоОбъект, СтруктураРеквизитов);
	
	ТекстПисьма = "Запланированные заявки и заявки в работе";
 
	Для Каждого Получатель Из ПолучателиОтчета Цикл
		АдресРаспределителя = РаботаСЗаявкамиИПочтой.ПолучитьАдресПользователя(Получатель);
		Если ЗначениеЗаполнено(АдресРаспределителя) Тогда
			НоваяСтр = ПисьмоОбъект.ПолучателиКопий.Добавить();
			НоваяСтр.Адрес = АдресРаспределителя;
			НоваяСтр.Контакт = Получатель;
			НоваяСтр.ОтображаемоеИмя = Получатель.Наименование;
		КонецЕсли;
	КонецЦикла;
	
	КодировкаИсходящихПисем = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"ВстроеннаяПочта",
		"КодировкаИсходящихПисем",
		"utf-8");
	ПисьмоОбъект.Кодировка = КодировкаИсходящихПисем;
	
	ПисьмоОбъект.ТекстХранилище			= Новый ХранилищеЗначения(ТекстПисьма, Новый СжатиеДанных);
	ПисьмоОбъект.ПодготовленоКОтправке	= ТекущаяДатаСеанса();
	
	// тема и текст оповещения 
	ПисьмоОбъект.Тема = "Отчет по завкам";
	
	ПисьмоОбъект.ТипТекста = Перечисления.ТипыТекстовПочтовыхСообщений.ПростойТекст;
	
	ПисьмоОбъект.Записать();
	
	Для Каждого СтрокаТЗ Из тзВложения Цикл
		Адрес = ПоместитьВоВременноеХранилище(СтрокаТЗ.Вложение.Получить(), Новый УникальныйИдентификатор() );
		ВстроеннаяПочтаСервер.ДобавитьВложениеПисьмаИзВременногоХранилища(
								ПисьмоОбъект.Ссылка, // Письмо
								Адрес, // АдресВременногоХранилища
								"", // АдресВременногоХранилищаТекста
								0,
								СтрокаТЗ.ИмяВложения,
								ТекущаяДата(), // ВремяИзменения
								Неопределено); // Идентификатор - идентификатор картинки
	КонецЦикла;
	
	//ПисьмоРазмер = СтрДлина(ПисьмоТекстHTML);
	//ПисьмоРазмерПредставление = РаботаСоСтроками.ПолучитьРазмерСтрокой(ПисьмоРазмер);
	//ИмяВременногоФайла = ПолучитьИмяВременногоФайла("html");
	//ЗаписьТекста = Новый ЗаписьТекста(ИмяВременногоФайла, ЗаявкаСсылка.ДокументОснование.Кодировка);
	//ЗаписьТекста.Записать(ПисьмоТекстHTML);
	//ЗаписьТекста.Закрыть();
	//Данные = Новый ДвоичныеДанные(ИмяВременногоФайла);
	//Адрес = ПоместитьВоВременноеХранилище(Данные, Новый УникальныйИдентификатор() );
	//ВстроеннаяПочтаСервер.ДобавитьВложениеПисьмаИзВременногоХранилища(
	//						ПисьмоОбъект.Ссылка, // Письмо
	//						Адрес, // АдресВременногоХранилища
	//						"", // АдресВременногоХранилищаТекста
	//						ПисьмоРазмер,
	//						"Original.html",
	//						ТекущаяДата(), // ВремяИзменения
	//						Неопределено); // Идентификатор - идентификатор картинки
	
	
	
	Возврат ПисьмоОбъект.Ссылка;
КонецФункции // ()

Функция ПолучитьПолучателейОтчетаПоЗаявкам() Экспорт 
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИсполнителиЗадач.Исполнитель
		|ИЗ
		|	РегистрСведений.ИсполнителиЗадач КАК ИсполнителиЗадач
		|ГДЕ
		|	ИсполнителиЗадач.РольИсполнителя = ЗНАЧЕНИЕ(Справочник.РолиИсполнителей.ПолучательОтчетаПоЗаявкам)
		|
		|СГРУППИРОВАТЬ ПО
		|	ИсполнителиЗадач.Исполнитель";
		
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Исполнитель");	
КонецФункции // ПолучитьПолучателейОтчетаПоЗаявкам()
